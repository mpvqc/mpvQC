# Copyright 2024
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

BASE_COLORS := '"#F44336" "#E91E63" "#9C27B0" "#673AB7" "#3F51B5" "#2196F3" "#03A9F4" "#00BCD4" "#009688" "#4CAF50" "#8BC34A" "#CDDC39" "#FFEB3B" "#FFC107" "#FF9800" "#FF5722" "#795548" "#9E9E9E" "#607D8B"'

_default:
    @just --list

# Print Material Dark palette
[group('Material')]
generate-dark-color-palette:
    @python color-generator.py --dark {{ BASE_COLORS }}

# Print Material Light palette
[group('Material')]
generate-light-color-palette:
    @python color-generator.py --contrast 0.0 {{ BASE_COLORS }}

# Print dark mpvQC colors
[group('mpvQC')]
dark-mpvQC-colors:
    #!/usr/bin/env nu
    just generate-dark-color-palette |
        from toml |
        get colors |
        each { |palette|
            {
                colors: [
                    {
                        "background": ($palette.surface),
                        "foreground": ($palette.onSurfaceVariant),
                        "control": ($palette.primary),
                        "row-highlight": ($palette.inversePrimary),
                        "row-highlight-text": ($palette.onSurface),
                        "row-base": ($palette.surface),
                    }
                ]
            }
        } |
        each { |row| $row | to toml } |
        str join "\n"

# Print light mpvQC colors
[group('mpvQC')]
light-mpvQC-colors:
    #!/usr/bin/env nu
    just generate-light-color-palette |
        from toml |
        get colors |
        each { |palette|
            {
                colors: [
                    {
                        "background": ($palette.surfaceContainerLow),
                        "foreground": ($palette.onSurfaceVariant),
                        "control": ($palette.secondary),
                        "row-highlight": ($palette.primary),
                        "row-highlight-text": ($palette.onPrimary),
                        "row-base": ($palette.surfaceContainerLow),
                    }
                ]
            }
        } |
        each { |row| $row | to toml } |
        str join "\n"
